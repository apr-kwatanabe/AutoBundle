// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply from: project.file('metadata.gradle')

final androidSdkPath = System.getenv('ANDROID_HOME') ?: {
    final localProp = new Properties()
    try {
        localProp.load(new FileInputStream("${rootProject.projectDir}/local.properties"))
    } catch (IOException ignore) {}
    localProp['sdk.dir'] ?: {
        throw new RuntimeException("Android sdk not found")
    }()
}()

final m2Repository = "${androidSdkPath}/extras/android/m2repository"

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

ext {
    sdkVersion = 28
    androidJar = fileTree(dir: "${androidSdkPath}/platforms/android-${sdkVersion}/", include:  "android.jar")
    supportLibraryVersion = '1.0.0'
}

allprojects {
    repositories {
        google()
        maven {
            url m2Repository
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}